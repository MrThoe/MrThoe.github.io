
<html>
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
        <title>Breakout Retro!</title>
        <script type="text/javascript" src="js/processing.js"></script>
        <link rel="stylesheet" href="./styles/styleGame.css">
    </head>
    
    <body>
        <script type="text/processing">
        
/** BREAKOUT RETRO!  --> PART 6  Sound ; )
  *
  * @Author:  ALLEN THOE
  * @Date: 12/15/2019
  */

//import processing.sound.*;
//SoundFile song;

Ball ball;
Paddle paddle;
Block[][] block;
int rows, columns, score, lives, level;


void setup(){
  size(800,600);
  textSize(24);
  level = 0;
  score=0;
  lives = 3;  
  rows = 4;
  columns = 4;
  makeLevel(columns, rows);  
  //song = new SoundFile(this, "song.mp3");
}

void draw(){
  background(0);
  ball.display();
  ball.checkPaddle(paddle);
  paddle.display();
  showBlocks(); 
  showLives();
  showScore();
  checkLevel();
}

void checkLevel(){
  if(clearBlocks()){
    level++;
    ball.canMove = false;
    ball.y = paddle.y - ball.d/2;
    fill(0);
    rect(190, height/2 + 130, 400, 30);
    rect(250, height/2 + 160, 400, 30);    
    fill(255,0,255);  
    text("You Cleared all the blocks!" , 200, height/2+152);
    text("Click anywhere to continue" , 260, height/2+182); 
    if(mousePressed){
      if(level%2==0){
        rows += 2;
      } else {
        columns += 2;
      }
      makeLevel(rows, columns);   
      ball.canMove = true;
    }
  }
}

void showScore(){
  strokeWeight(2);
  fill(255,0,255);
  text("Score: " + score, width - 140, height - 10); 
}
void showLives(){
  fill(255,0,255);  
  text("Lives: " + lives, 40, height - 10);  
  if(lives == 0){
    fill(0);
    rect(190, height/2 + 130, 400, 30);
    rect(250, height/2 + 160, 400, 30);    
    fill(255,0,255);  
    text("Sorry, you ran out of lives...." , 200, height/2+152);
    text("Click anywhere to restart" , 260, height/2+182);    
  }
}

void showBlocks(){
  for(int i = 0; i < block.length; i++){
    for(int j = 0; j < block[0].length; j++){
      block[i][j].display();
      block[i][j].checkBall(ball);
    }
  }  
}

void makeLevel(int rows, int columns){
  ball = new Ball();
  paddle = new Paddle();
  block = new Block[rows][columns];
  for(int i = 0; i < block.length; i++){
    for(int j = 0; j < block[0].length; j++){
      block[i][j] = new Block(i,j+5,block.length);
    }
  }  
}

boolean clearBlocks(){
  for(int i = 0; i < block.length; i++){
    for(int j = 0; j < block[0].length; j++){
      if(block[i][j].status){
        return false;  
      }
    }
  }
  return true;
}

void mousePressed(){
  if(lives > 0){
    ball.y -= 5;
    ball.canMove = true; 
  } else {
    setup();
  }
  //song.loop();
}

public class Ball{
  private float x, Vx;
  private float y, Vy;
  private float d;
  private boolean canMove;
  private String edge;
    
  public Ball(){
    x = width/2;
    d = 28;  
    y = height - 60 - d/2;
    Vx = random(-5,5);
    Vy = -7;

  }
  
  public void display(){
    if(canMove){
      x += Vx;
      y += Vy;
      //checkWalls
      if(x < 5){
        Vx *= -1;
        x = 5;
      }      
      if(x > width-5){
        Vx *= -1;
        x = width-5;
      }
      if(y < 5){
        Vy *= -1;
      } else if (y > height - d/2){
        canMove = false;
        y = height - 60 - d/2;
        lives--;
      }
    } else {
      x = mouseX;
    }
    fill(155);
    ellipse(x,y,d+10,d+10);    
    fill(200,170,0);
    ellipse(x,y,d,d);
  }
  
  public void checkPaddle(Paddle pad){
      if(x > pad.x && x < pad.x + pad.w && y > pad.y - d/2 && y < pad.y+2){
        //Vy *= -1;
        Vx += (x - mouseX)/10;
       //CAP THE VX
        if (Vx > 8){
          Vx = 8;
        }
        if (Vx < -8){
          Vx = -8;
        }
        if(Vx < 0){
          Vy = -12 - Vx;
        } else {
          Vy = -12 + Vx;
        }
      } 

  }
}

public class Block{
    private float x, y, w, h;
    private int numBlocks;  //CHANGED FROM EPISODE 3
    private boolean status;
    private int r, g, b;
    
    public Block(){
      x = 0;
      numBlocks = 8;
      y = 0;
      w = width / numBlocks;
      h = 30;
    }
    public Block(int col, int row, int theNumBlocks){
      numBlocks = theNumBlocks;
      w = width / numBlocks;
      h = 30;
      x = w * col;
      y = h * row;
      setColors(row, col);
      status = true;
    }
      
    public void setColors(int col, int row){
        b = 150;
        if(col%9==0){
          r = 50;
        } else if (col%9==1){
          r = 100;
        } else if (col%9==2){
          r = 150;
        } else if (col%9==3){
          r = 200;
        } else if (col%9==4){ 
          r = 250;
        } else if (col%9==5){
          r = 200;
        } else if (col%9==6){
          r = 150;
        } else if (col%9==7){
          r = 100;
        } else if (col%9==8){ 
          r = 50;
        }
        
        if(row%2==0){
          g = 70;
          b = 150;
        }
    }
    public void display(){
      stroke(0);
      strokeWeight(1);
      fill(r/2,g/2,b/2);
      if(status){
        rect(x,y, w, h, 10);
        fill(255,255,0);
        rect(x+8, y+8, w-16, h-16, 2);
        fill(r,g,b);
        rect(x+10, y+10, w-20, h-20, 3);
        
      }
    }
    
    public void checkBall(Ball ball){
       if(status){
         //Bottom
         if(ball.x > x && ball.x < x+w && ball.y < (y+h+ball.d/2)&& ball.y>y+h){
           ball.Vy*=-1;
           status=false;
           score++;
         }
         //Top
         if(ball.x > x && ball.x < x+w && ball.y > y-ball.d/2 && ball.y < y){
           ball.Vy*=-1;
           status=false;
           score++;
         }
         //Left
         if(ball.x > x - ball.d/2 && ball.y > y && ball.y < y+h && ball.x < x){
           ball.Vx*=-1;
           status=false;
           score++;
         }
         //Right
         if(ball.x > x+w  && ball.y > y && ball.y < y+h && ball.x<x+w+ball.d/2){
           ball.Vx*=-1;
           status=false;
           score++;
         }                
       }
      
    }
}
public class Paddle{
   //Variables (x,y, width, height (rectangle))
   private float x, y, w, h;
   
   public Paddle(){
     x = width/2;
     y = height - 60;
     w = 200;
     h = 20;
   }
   
   public void display(){
    
     x = mouseX - w/2;  //CHANGED FROM EPISODE 3

     fill(255,255,0); //
     rect(x-3,y-3,w+6,h+6,8);     
     fill(0,50,100);
     rect(x,y,w,h,8); 
     stroke(155,0,55);
     strokeWeight(3);     
     line(x+3,y+h/2,x+w-3,y+h/2);
     line(x+w/3, y+2, x+w/3, y+h-2);
     line(x+2*w/3, y+2, x+2*w/3, y+h-2);     
   }
  
  
}

        </script>
      
        <canvas id="sketch" style="border: 1px solid black;"></canvas>
        <h1>  ~Welcome to Breakout Retro~</h1>
        <h2> Use the mouse to play </h2>
        <h2>  Author: Allen Thoe </h2>
        <h2> Language: Processing (java)</h2>
        <hr>
        <h3>  Music: <i class="blue">Summer</i> -- Courtesy of <a href="https://www.bensound.com/"> bensound.com </a></h3>        
        <h3> Click Play for music with the game </h3>
               <hr>
        <audio controls>

          <source src="./music/summer.mp3" type="audio/mpeg">
          Your browser does not support the audio tag.
        </audio>        
        <hr>
        <h1>Complete Tutorial avaialable!</h1>
        <h3>If you want to build this in PROCESSING (java), check out the video below </h3>
        <iframe width="560" height="315" src="https://www.youtube.com/embed/l_HBdZxoB1k" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
    </body>
</html>